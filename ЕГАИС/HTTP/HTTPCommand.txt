ОтветСервера		= "";
IF ( !ЕГАИС.HTTPConnect( ЕГАИССЕРВЕР, ЕГАИСПОРТ, true ) ) 
{
	СООБЩЕНИЕ( "Не удалось подключиться к транспортному модулю. Проверьте параметры соединения (" + ALLTRIM( ЕГАИССЕРВЕР ) + ":" + ALLTRIM( ЕГАИСПОРТ ) + ")." );
	RETURN false;
}

СообщениеОбОшибке	= "";
IF ( ALLTRIM( UPPER( Команда ) ) == "GET" )
	ОтветСервера		= ЕГАИС.HTTPGet( mURL, @СообщениеОбОшибке );
ELSE IF ( ALLTRIM( UPPER( Команда ) ) == "POST" )
	ОтветСервера		= ЕГАИС.HTTPPost( mURL, СтрокаДляОтправки, @СообщениеОбОшибке, ИмяФайла );
ELSE IF ( ALLTRIM( UPPER( Команда ) ) == "DELETE" )
	ОтветСервера		= ЕГАИС.HTTPDelete( mURL, @СообщениеОбОшибке );
ELSE
	СообщениеОбОшибке	= "Неизвестная команда " + Команда;
	
ЕГАИС.HTTPDisconnect( );

// Если вернулся пустой ответ, то это означает ошибку
IF ( ПУСТО( ОтветСервера ) )
{
	IF ( !ПУСТО( СообщениеОбОшибке ) )
	{
		ИмяОперации		= "загрузке";
		IF ( ALLTRIM( UPPER( Команда ) ) == "POST" )
		{
			ИмяОперации	= "отправке";
			
			// Записываем отправляемый текст в журнал ошибок отправки
			ФайлОшибки	= ФАЙЛОТКРЫТЬ( "utmposterror.log", 1, 1 );
			ФАЙЛУСТАНОВИТЬУКАЗАТЕЛЬ( ФайлОшибки, 0, 0, 2 );
			ФАЙЛЗАПИСАТЬСТРОКУ( ФайлОшибки, REPLICATE( "*", "80" ) );
			ФАЙЛЗАПИСАТЬСТРОКУ( ФайлОшибки, TTOC( ДАТАВРЕМЯ( ) ) + ": " + ALLTRIM( ЕГАИССЕРВЕР ) + ":" + ALLTRIM( ЕГАИСПОРТ ) + mURL );
			ФАЙЛЗАПИСАТЬСТРОКУ( ФайлОшибки, "Ответ УТМ: " + ПЕРЕКОДИРОВАТЬ( HTTPResponse, "UTF-8", "ANSI" ) );
			ФАЙЛЗАПИСАТЬСТРОКУ( ФайлОшибки, СтрокаДляОтправки );
			ФАЙЛЗАКРЫТЬ( ФайлОшибки );
			
			IF ( СООБЩЕНИЕ( "При " + ИмяОперации + " файла" + CHR( 13 ) + mURL + CHR( 13 ) + "транспортный модуль ЕГАИС вернул собщение об ошибке." + CHR( 13 ) + CHR( 13 ) + СообщениеОбОшибке + CHR( 13 ) + ПЕРЕКОДИРОВАТЬ( HTTPResponse, "UTF-8", "ANSI" ) + CHR( 13 ) + CHR( 13 ) + "Показать отправляемый в УТМ текст?", "Отправка документа", 4 ) == 6 )
				СООБЩЕНИЕ( "URL: " + mURL + CHR( 13 ) + ПЕРЕКОДИРОВАТЬ( СтрокаДляОтправки, "UTF-8", "ANSI" ) );
			RETURN false;
		}
		ELSE IF ( ALLTRIM( UPPER( Команда ) ) == "DELETE" )
			ИмяОперации	= "удалении";

		СООБЩЕНИЕ( "При " + ИмяОперации + " файла" + CHR( 13 ) + mURL + CHR( 13 ) + "транспортный модуль ЕГАИС вернул собщение об ошибке." + CHR( 13 ) + СообщениеОбОшибке + CHR( 13 ) + ПЕРЕКОДИРОВАТЬ( HTTPResponse, "UTF-8", "ANSI" ), "Загрузка документа" );
		RETURN false;
	}
	// GET должен возвращать результат. Дргуие команды не обязательно
	ELSE IF ( ALLTRIM( UPPER( Команда ) ) == "GET" )
	{
		СООБЩЕНИЕ( "Нет данных для загрузки. URL " + mURL, "Загрузка документа" );
		RETURN false;
	}
}

RETURN true;
