
// Если документ ставит на баланс на розничный склад ЕГАИС, то вызываем процедуру постановки на розничный склад магазина ЕГАИС
IF ( LOWER( ЗАПРОС( "SELECT egaisreg FROM spr003 WHERE identity_column= " + ИДДокумента ) ) == "розничный" )
	RETURN ЕГАИС.ОтправитьАКТСПИСАНИЯПРОДУКЦИИМАГАЗИНА_В2( ИДДокумента );

ЕГАИСИД			= ЗАПРОС( "SELECT value FROM param_ex WHERE param = 'EGAIS_ID" + СКЛАД + "'" );
ДОБАВИТЬКОНТЕКСТ( "SELECT spr003.ndok, spr003.date, spr003.note, spr003.identity_column, spr003.cause, spr003.f_skipcheckinformb
					FROM spr003 spr003
					INNER JOIN sprfirm sprfirm ON sprfirm.code = spr003.firm
					WHERE spr003.identity_column = " + ИДДокумента, "ДокументИнфо" );

НомерДокумента			= ЕГАИС.ЗаменитьСимволы( ДокументИнфо.ndok );
ПримечаниеКДокументу	= ЕГАИС.ЗаменитьСимволы( ДокументИнфо.note );
ПричинаСписания			= ЕГАИС.ЗаменитьСимволы( ДокументИнфо.cause );
НеПроверятьСправкуБ		= ДокументИнфо.f_skipcheckinformb;
ТекстАкта		= 
"<?xml version=""1.0"" encoding=""UTF-8""?>
	<ns:Documents Version=""1.0""
		xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance""
		xmlns:ns= ""http://fsrar.ru/WEGAIS/WB_DOC_SINGLE_01""
		xmlns:pref=""http://fsrar.ru/WEGAIS/ProductRef_v2""
		xmlns:awr=""http://fsrar.ru/WEGAIS/ActWriteOff_v3""
		xmlns:ce=""http://fsrar.ru/WEGAIS/CommonV3"">
		<ns:Owner>
			<ns:FSRAR_ID>" + ЕГАИСИД + "</ns:FSRAR_ID>
		</ns:Owner>
		<ns:Document>
			<ns:ActWriteOff_v3>
				<awr:Identity>" + ДокументИнфо.identity_column + "</awr:Identity>
				<awr:Header>
					<awr:ActNumber>" + НомерДокумента + "</awr:ActNumber>
					<awr:ActDate>" + DTOC( ДокументИнфо.date, 7,  "-" ) + "</awr:ActDate>
					<awr:TypeWriteOff>" + ПричинаСписания + "</awr:TypeWriteOff>
					<awr:Note>" + ПримечаниеКДокументу + "</awr:Note>
				</awr:Header>
				<awr:Content>
";
УДАЛИТЬКОНТЕКСТ( "ДокументИнфо" );

// Выбираем строки накладной для передачи в ЕГАИС
ВыражениеЦеныСписания	= ЕСЛИ( ТИП( "_ЕГАИСКАТЕГОРИЯЦЕНЫСПИСАНИЯ" ) == "C", "CASE WHEN '" + _ЕГАИСКАТЕГОРИЯЦЕНЫСПИСАНИЯ + "' = '' THEN (SELECT cena_sale FROM sprres WHERE code = spec.nn) ELSE dbo.fn_calcclev_cena( spec.nn, spr.date, '" + _ЕГАИСКАТЕГОРИЯЦЕНЫСПИСАНИЯ + "', '001' ) END", 
																		     "dbo.fn_calcsklad_cena( spec.nn, spr.date, spr.sklad, '001' )" );
НайденыОшибки			= false;
ДОБАВИТЬКОНТЕКСТ( "SELECT spec.nn, spec.npp, barcodes.kolp, barcodes.barcode, spec.identity_column, " + ВыражениеЦеныСписания + " * barcodes.kolp AS summa
				   FROM spec003 spec
				   INNER JOIN spr003 spr ON spr.identity_column = spec.ic
				   INNER JOIN barcodes2 barcodes ON spec.identity_column = barcodes.spec_ic AND barcodes.code = '003' AND barcodes.ic = spec.ic
				   WHERE spec.ic = " + ИДДокумента + " AND barcodes.kolp > 0
				   ORDER BY dbo.fn_chartoint( npp )", "СписокСтрок" );
WHILE ( !КОНЕЦКОНТЕКСТА( "СписокСтрок" ) )
{
	IF ( AT( СписокСтрок.barcode, "/" ) > 0 )
		СправкаБ	= ЕГАИС.ЗаменитьСимволы( ALLTRIM( ПОЛУЧИТЬСЛОВО( "/" + СписокСтрок.barcode, "/", "/", 2 ) ) );
	ELSE
		СправкаБ	= СписокСтрок.barcode;

	ТекстАкта	+= 
"
					<awr:Position>
						<awr:Identity>" + STR( НОМЕРСТРОКИ( "СписокСтрок" ) + 1 ) + "</awr:Identity>
						<awr:Quantity>" + STR( СписокСтрок.kolp, 10, 4 ) + "</awr:Quantity><awr:SumSale>" + STR( СписокСтрок.summa, 16, 2 ) + "</awr:SumSale>
";

	// В зависимости от того, какие поля указаны формируем разные разделы для справки А и Б
	ТекстАкта	+= 
"						<awr:InformF1F2>
							<awr:InformF2>
								<pref:F2RegId>" + СправкаБ + "</pref:F2RegId>
							</awr:InformF2>
						</awr:InformF1F2>";
	// Теперь выводим акцизные марки, если были указаны
	ДОБАВИТЬКОНТЕКСТ( "SELECT barcode 
					   FROM barcodes mainbc
					   WHERE mainbc.code = '003' AND mainbc.spec_ic = " + СписокСтрок.identity_column + 
					   ЕСЛИ( НеПроверятьСправкуБ, "", " AND dbo.fn_informbformark( mainbc.barcode, 0 ) = '" + STDF( СправкаБ ) + "'" ), "СписокМарок" );
					   
	ДолжныБытьМарки	= ЗАПРОС( "SELECT bcexists FROM egais_marks WHERE informb LIKE '%/" + STDF( СправкаБ ) + "'" );
	IF ( ДолжныБытьМарки && КОЛИЧЕСТВОСТРОК( "СписокМарок" ) < СписокСтрок.kolp )
	{
		ТекстСообщенияПроМарки		+= CHR( 13 ) + ЗАПРОС( "SELECT RTRIM( name ) + ' (' + RTRIM( maincode ) + ')' FROM sprnn WHERE nn= '" + СписокСтрок.nn + "'" );
		НайденыОшибки				= true;
		ПРОПУСТИТЬ( 1, "СписокСтрок" );
		CONTINUE;
	}

	IF ( КОЛИЧЕСТВОСТРОК( "СписокМарок" ) > 0 )
	{
		ТекстАкта	+= 
"						<awr:MarkCodeInfo>
";
		WHILE ( !КОНЕЦКОНТЕКСТА( "СписокМарок" ) )
		{
			ТекстАкта	+= 
"							<ce:amc>" + ЕГАИС.ЗаменитьСимволы( СписокМарок.barcode ) + "</ce:amc> 
";
			ПРОПУСТИТЬ( 1,  "СписокМарок" );
		}
		УДАЛИТЬКОНТЕКСТ( "СписокМарок" );
		ТекстАкта	+= 
"						</awr:MarkCodeInfo>		
"
	};						
		ТекстАкта	+= 
"					</awr:Position>
";
	ПРОПУСТИТЬ( 1, "СписокСтрок" );
}
УДАЛИТЬКОНТЕКСТ( "СписокСтрок" ); 
IF ( НайденыОшибки ) RETURN false;

ТекстАкта		+=
"				</awr:Content>
			</ns:ActWriteOff_v3>
		</ns:Document>
	</ns:Documents>";
				
// Отправляем получившийся текст на сервер
ОтветСервера = "";
IF ( !ЕГАИС.HTTPCommand( "POST", "/opt/in/ActWriteOff_v3", ПЕРЕКОДИРОВАТЬ( ТекстАкта, "ANSI", "UTF-8" ), @ОтветСервера, "ActWriteOff_v3.xml" ) ) RETURN false; 

ДОБАВИТЬКОНТЕКСТ( "SET FMTONLY ON SELECT CONVERT( char( 250 ), '' ) AS url SET FMTONLY OFF", "АктUrl" );
ЗАГРУЗИТЬ( "АктUrl", "XMLSTRING", ОтветСервера, "url" );	

// Если все благополучно отправлено, то сохраняем информацию в документе
ЗАПРОС( "UPDATE spr003 SET egaisstatus = 2, egaisreplyid = '" + АктUrl.url + "', egaiscomment= '' WHERE identity_column = " + ИДДокумента );
ДОБАВИТЬКОНТЕКСТ( "INSERT INTO spec000_img ( code, ic, pnum, filename, doc_image ) 
				   VALUES ( '003', " + ИДДокумента + ", 0, 'АктКвитанцияУТМ.txt', CONVERT( varbinary( max ), '" + STDF( ОтветСервера ) + "' ) )", "ПриложениеКДокументу" );
УДАЛИТЬКОНТЕКСТ( "ПриложениеКДокументу" );
УДАЛИТЬКОНТЕКСТ( "АктUrl" );
ЕГАИС.ДобавитьЗапрос( 7, "003", ИДДокумента, ТекстАкта, ОтветСервера );

RETURN true;
