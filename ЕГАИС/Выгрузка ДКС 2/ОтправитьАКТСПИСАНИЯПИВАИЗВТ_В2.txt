
// Сначала распределяем номера справок. Без них не определить алкокоды
IF ( !ЕГАИС.РаспределитьАлкокодыВТ( ИДДокумента, Склад ) ) RETURN false;

ОтправлятьМаркированный		= ЕСЛИ( ТИП( "_ЕГАИСОТПРАВЛЯТЬМАРКИРОВАННЫЙПОВТ" ) == "L", _ЕГАИСОТПРАВЛЯТЬМАРКИРОВАННЫЙПОВТ, false );
ОтправлятьДатойДокумента	= ЕСЛИ( ТИП( "_ЕГАИСОТПРАВЛЯТЬДКСДАТОЙДОКУМЕНТА" ) == "L", _ЕГАИСОТПРАВЛЯТЬДКСДАТОЙДОКУМЕНТА, false );

ЕГАИСИД						= ЗАПРОС( "SELECT value FROM param_ex WHERE param = 'EGAIS_ID" + СКЛАД + "'" );
СПИСЫВАТЬМАРКИРОВАННЫЙ		= VAL( ЗАПРОС( "SELECT value FROM param_ex WHERE param = 'EGAIS_MARKSPS" + СКЛАД + "'" ) ) <> 0 OR ОтправлятьМаркированный;

ДОБАВИТЬКОНТЕКСТ( "SELECT spr.ndok, spr.date, spr.note, spr.identity_column
					FROM spr046 spr
					WHERE spr.identity_column = " + ИДДокумента, "ДокументИнфо" );

НомерДокумента			= ЕГАИС.ЗаменитьСимволы( 'ВТ-' + ДокументИнфо.ndok );
ПримечаниеКДокументу	= "Реализация";
ПричинаСписания			= "Реализация";
ТекстАкта		= 
"<?xml version=""1.0"" encoding=""UTF-8""?>
	<ns:Documents Version=""1.0""
		xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance""
		xmlns:ns= ""http://fsrar.ru/WEGAIS/WB_DOC_SINGLE_01""
	    xmlns:oref=""http://fsrar.ru/WEGAIS/ClientRef_v2""
		xmlns:pref=""http://fsrar.ru/WEGAIS/ProductRef_v2""
		xmlns:awr=""http://fsrar.ru/WEGAIS/ActWriteOffShop_v2""
		xmlns:ce=""http://fsrar.ru/WEGAIS/CommonEnum"">
		<ns:Owner>
			<ns:FSRAR_ID>" + ЕГАИСИД + "</ns:FSRAR_ID>
		</ns:Owner>
		<ns:Document>
			<ns:ActWriteOffShop_v2>
				<awr:Identity>" + ДокументИнфо.identity_column + "</awr:Identity>
				<awr:Header>
					<awr:ActNumber>" + НомерДокумента + "</awr:ActNumber>
					<awr:ActDate>" + DTOC( ЕСЛИ( ОтправлятьДатойДокумента, ДокументИнфо.date, ДАТА( ) ), 7,  "-" ) + "</awr:ActDate>
					<awr:TypeWriteOff>" + ПричинаСписания + "</awr:TypeWriteOff>
					<awr:Note>" + ПримечаниеКДокументу + "</awr:Note>
				</awr:Header>
				<awr:Content>
";
УДАЛИТЬКОНТЕКСТ( "ДокументИнфо" );

// Выбираем строки накладной для передачи в ЕГАИС
// В литрах и дал учитывается разливной алкоголь. Поэтому, если в карточке товара единица измерения литр или дал, то необходимо преобразовать количество в декалитры
// В противном случае, преобразуем количество в штуки, возможно, что через объем тары.
ВыражениеЦеныСписания	= ЕСЛИ( ТИП( "_ЕГАИСКАТЕГОРИЯЦЕНЫСПИСАНИЯ" ) == "C", "CASE WHEN '" + _ЕГАИСКАТЕГОРИЯЦЕНЫСПИСАНИЯ + "' = '' THEN (SELECT cena_sale FROM sprres WHERE code = spec.nn) ELSE dbo.fn_calcclev_cena( spec.nn, spr.date, '" + _ЕГАИСКАТЕГОРИЯЦЕНЫСПИСАНИЯ + "', '001' ) END", 
																				   "dbo.fn_calcsklad_cena( spec.nn, spr.date, spr.sklad, '001' )" );

ДОБАВИТЬКОНТЕКСТ( "SELECT *, kolp * cena AS summa FROM ( SELECT spec.nn, " + ВыражениеЦеныСписания + " AS cena, MAX( spec.identity_column ) AS identity_column, 
						  SUM(  CASE WHEN egais.packed = 0 OR ( egais.packed = -1 AND sprnn.ed IN ( '" + ЕГАИСЕДИНИЦАЛИТРЫ + "', 'дал', 'дкл' ) )
								THEN spec.kolp * 
										CASE WHEN spec.ed = '" + ЕГАИСЕДИНИЦАЛИТРЫ + "' THEN 0.1 ELSE
										CASE WHEN spec.ed IN ( 'дал', 'дкл' ) THEN 1 ELSE sprnn.d_litr / 10 END END 
								ELSE spec.kolp /
										CASE WHEN spec.ed = '" + ЕГАИСЕДИНИЦАЛИТРЫ + "' THEN sprnn.d_litr ELSE 
										CASE WHEN spec.ed IN ( 'дал', 'дкл' ) THEN sprnn.d_litr / 10 ELSE 1 END END END ) AS kolp, CONVERT( bit, 0 ) AS unpacked, spec.alcocode
				   FROM spec046 spec
				   INNER JOIN spr046 spr ON spr.identity_column = spec.ic
				   INNER JOIN sprnn sprnn ON sprnn.nn = spec.nn
				   INNER JOIN sprres_egais egais ON spec.alcocode = egais.alcocode
				   WHERE spec.ic = " + ИДДокумента + " AND dbo.fn_alcotype( sprnn.nnvid ) " + ЕСЛИ( СПИСЫВАТЬМАРКИРОВАННЫЙ, "<> 0", "= 2" ) + " AND spec.alcocode <> '' AND
						(( dbo.fn_alcotype( sprnn.nnvid ) = 1 AND EXISTS( SELECT * FROM barcodes 
																		  WHERE barcodes.code = '046' AND barcodes.spec_ic = spec.identity_column AND 
																				ISNULL( (SELECT bcexists FROM egais_marks WHERE mark = barcodes.barcode ), 0 ) = 0 ) ) OR
						 ( dbo.fn_alcotype( sprnn.nnvid ) = 2 AND NOT EXISTS( SELECT * FROM barcodes 
																			  WHERE barcodes.code = '046' AND barcodes.spec_ic = spec.identity_column AND 
																					ISNULL( (SELECT bcexists FROM egais_marks WHERE mark = barcodes.barcode ), 0 ) = 1 ) ) )
				   GROUP BY spec.nn, spec.alcocode, spr.date, spr.sklad ) AS alcoinfo
				   WHERE kolp > 0 
				   ORDER BY identity_column", "СписокСтрок" );
IF ( КОЛИЧЕСТВОСТРОК( "СписокСтрок" ) == 0 )
{
	ТекстСообщения		+= CHR( 13 ) + ЗАПРОС( "SELECT '№ ' + RTRIM( ndok ) + '/П от ' + CONVERT( char( 10 ), date, 104 ) FROM spr046 WHERE identity_column = " + ИДДокумента );
	RETURN true;
}

WHILE ( !КОНЕЦКОНТЕКСТА( "СписокСтрок" ) )
{
	ДОБАВИТЬКОНТЕКСТ( "SELECT barcodes.barcode 
					   FROM barcodes barcodes
					   INNER JOIN spec046 spec046 ON barcodes.spec_ic = spec046.identity_column 
					   INNER JOIN sprnn sprnn ON spec046.nn = sprnn.nn 
					   WHERE barcodes.code = '046' AND spec046.alcocode = '" + СписокСтрок.alcocode + "' AND spec046.ic = " + ИДДокумента + " AND dbo.fn_alcotype( nnvid ) = 1 AND 
							 ISNULL( (SELECT bcexists FROM egais_marks WHERE mark = barcodes.barcode ), 0 ) = 0", "СписокМарок" );
	ТекстАкта	+= 
"
					<awr:Position>
						<awr:Identity>" + STR( НОМЕРСТРОКИ( "СписокСтрок" ) + 1 ) + "</awr:Identity>
						" + ЕГАИС.ВыгрузитьТовар_В2( СписокСтрок.alcocode, "awr:Product", СписокСтрок.nn, СписокСтрок.unpacked ) + "
						<awr:Quantity>" + STR( ЕСЛИ( КОЛИЧЕСТВОСТРОК( "СписокМарок" ) > 0, КОЛИЧЕСТВОСТРОК( "СписокМарок" ), СписокСтрок.kolp ), 10, 3 ) + "</awr:Quantity><awr:SumSale>" + STR( СписокСтрок.summa, 16, 2 ) + "</awr:SumSale>";
	// Теперь выводим акцизные марки, если были указаны
	IF ( КОЛИЧЕСТВОСТРОК( "СписокМарок" ) > 0 )
	{
		ТекстАкта	+= 
"						<awr:MarkCodeInfo>
";
		WHILE ( !КОНЕЦКОНТЕКСТА( "СписокМарок" ) )
		{
			ТекстАкта	+= 
"							<MarkCode>" + ЕГАИС.ЗаменитьСимволы( СписокМарок.barcode ) + "</MarkCode> 
";
			ПРОПУСТИТЬ( 1,  "СписокМарок" );
		}
		УДАЛИТЬКОНТЕКСТ( "СписокМарок" );
		ТекстАкта	+= 
"						</awr:MarkCodeInfo>		
"
	};
ТекстАкта	+= 
"					</awr:Position>
";
	ПРОПУСТИТЬ( 1, "СписокСтрок" );
}
УДАЛИТЬКОНТЕКСТ( "СписокСтрок" ); 

ТекстАкта		+=
"				</awr:Content>
			</ns:ActWriteOffShop_v2>
		</ns:Document>
	</ns:Documents>";
				
// Отправляем получившийся текст на сервер
ОтветСервера = "";
IF ( !ЕГАИС.HTTPCommand( "POST", "/opt/in/ActWriteOffShop_v2", ПЕРЕКОДИРОВАТЬ( ТекстАкта, "ANSI", "UTF-8" ), @ОтветСервера, "ActWriteOffShop_v2.xml" ) ) RETURN false; 

ДОБАВИТЬКОНТЕКСТ( "SET FMTONLY ON SELECT CONVERT( char( 250 ), '' ) AS url SET FMTONLY OFF", "АктUrl" );
ЗАГРУЗИТЬ( "АктUrl", "XMLSTRING", ОтветСервера, "url" );	

// Если все благополучно отправлено, то сохраняем информацию в документе
ЗАПРОС( "UPDATE spr046 SET egaisstatus = 2, egaisdate= GETDATE( ), egaisreplyid = '" + АктUrl.url + "', egaiscomment= '' WHERE identity_column = " + ИДДокумента );
ДОБАВИТЬКОНТЕКСТ( "INSERT INTO spec000_img ( code, ic, pnum, filename, doc_image ) 
				   VALUES ( '046', " + ИДДокумента + ", 0, 'АктКвитанцияУТМ.txt', CONVERT( varbinary( max ), '" + STDF( ОтветСервера ) + "' ) )", "ПриложениеКДокументу" );
УДАЛИТЬКОНТЕКСТ( "ПриложениеКДокументу" );
УДАЛИТЬКОНТЕКСТ( "АктUrl" );
ЕГАИС.ДобавитьЗапрос( 7, "046", ИДДокумента, ТекстАкта, ОтветСервера );

RETURN true;
