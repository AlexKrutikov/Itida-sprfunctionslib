// По коду предприятия необходимо определить систему ЭДО, с которой работает пользователь.
// В зависимости от системы вызвать нужную функцию.

СИСТЕМАЭДО		= ЗАПРОС( "SELECT value FROM param_ex WHERE param= 'EDI_EDI" + ПРЕДПРИЯТИЕ + "'" );
СООБЩЕНИЯ		= "";
СООБЩЕНИЯСТАТУС	= "";
//------ Читаем документы из локальной таблицы
ДОБАВИТЬКОНТЕКСТ( "SELECT guid, rid FROM " + ИМЯТАБЛИЦЫ, "СписокДокументов" );
IF ( КОЛИЧЕСТВОСТРОК( "СписокДокументов" ) == 0 )
{
	СООБЩЕНИЕ( "Не было отмечено ни одного документа в списке." );
	RETURN true;
}

ПОКА ( !КОНЕЦКОНТЕКСТА( "СписокДокументов" ) )
{
	ИДДокумента	= ЗАПРОС( "SELECT guid, editionguid, ndok, date, status FROM ediincome WHERE rid = '" + СписокДокументов.rid + "'" );
	ИДРедакции	= ЗАПРОС( "", "editionguid" );
	Статус		= ЗАПРОС( "", "status" );
	IF ( ПУСТО( ИДДокумента ) ) 
	{
		НомерДокумента	= ЗАПРОС( "", "ndok" );
		ДатаДокумента	= ЗАПРОС( "", "date" );
		СООБЩЕНИЯ		+= CHR( 13 ) + "№ " + НомерДокумента + " от " + DTOC( ДатаДокумента );
		ПРОПУСТИТЬ( 1, "СписокДокументов" );
		CONTINUE;
	}
	IF ( Статус <> 0 ) 
	{
		НомерДокумента	= ЗАПРОС( "", "ndok" );
		ДатаДокумента	= ЗАПРОС( "", "date" );
		СООБЩЕНИЯСТАТУС	+= CHR( 13 ) + "№ " + НомерДокумента + " от " + DTOC( ДатаДокумента );
		ПРОПУСТИТЬ( 1, "СписокДокументов" );
		CONTINUE;
	}
	IF ( СИСТЕМАЭДО == "СБИС" )
	{
		IF ( !ЭДО.СБИСОтправитьПодтвержедение( ПРЕДПРИЯТИЕ, ИДДокумента, ИДРедакции ) ) RETURN false;
	}
	ELSE IF ( СИСТЕМАЭДО == "ТАКСКОМ" )
	{
		IF ( !ЭДО.ТАКСКОМОтправитьПодтвержедение( ПРЕДПРИЯТИЕ, ИДДокумента ) ) RETURN false;
	}

	ОЖИДАНИЕ( 500 );
	ПРОПУСТИТЬ( 1, "СписокДокументов" );
}
IF ( !ПУСТО( СООБЩЕНИЯ ) )
	СООБЩЕНИЕ( "Следующие документы не были получены из системы ЭДО. Подтверждения по ним не были отправлены." + CHR( 13 ) + СООБЩЕНИЯ );
IF ( !ПУСТО( СООБЩЕНИЯСТАТУС ) )
	СООБЩЕНИЕ( "Статус следующих документов не позволяет отправить их в систему ЭДО. Подтверждения по ним не были отправлены." + CHR( 13 ) + СООБЩЕНИЯСТАТУС );

IF ( ПУСТО( СООБЩЕНИЯ ) && ПУСТО( СООБЩЕНИЯСТАТУС ) )
	СООБЩЕНИЕ( "Подтверждения по документам были успешно отправлены" );

УДАЛИТЬКОНТЕКСТ( "СписокДокументов" );
RETURN true;
