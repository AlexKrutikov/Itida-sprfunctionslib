PUBLIC ИмяТаблицыСтрокЧека, _КОЛИЧЕСТВОСИМВОЛОВЧЕКОВОЙЛЕНТЫ, _ШРИФТТЕКСТАДЛЯНЕФИСКАЛЬНОЙПЕЧАТИ, _ЭТОКОПИЯЧЕКА;

_ЭТОКОПИЯЧЕКА = true;

ЭТОРМК = false;			
IF (TYPE("_МРП") <> "U")
	ЭТОРМК = _МРП == "2";

IF ( МОДЕЛЬОБОРУДОВАНИЯ == "АТОЛДТО10" )
{
	ФР.АТОЛДТО10_СоответствиеПРСР();
	_ШРИФТТЕКСТАДЛЯНЕФИСКАЛЬНОЙПЕЧАТИ = 0;
	_КОЛИЧЕСТВОСИМВОЛОВЧЕКОВОЙЛЕНТЫ = ФР.АТОЛДТО10_ШиринаЛенты(ФР.КОДОБОРУДОВАНИЯ());
}
ELSE IF ( МОДЕЛЬОБОРУДОВАНИЯ == "МЕРКУРИЙ" )	
{
	ФР.МЕРКУРИЙФР_СоответствиеПРСР();
	_КОЛИЧЕСТВОСИМВОЛОВЧЕКОВОЙЛЕНТЫ = ФР.МЕРКУРИЙФР_ШиринаЛенты(ФР.КОДОБОРУДОВАНИЯ());
}
ELSE IF ( МОДЕЛЬОБОРУДОВАНИЯ == "ШТРИХ" )	
{
	ФР.ШТРИХ_СоответствиеПРСР();
	_КОЛИЧЕСТВОСИМВОЛОВЧЕКОВОЙЛЕНТЫ = ФР.ШТРИХ_ШиринаЛенты(ФР.КОДОБОРУДОВАНИЯ());
}

ИмяТаблицыСтрокЧека						= "##" + УНИКАЛЬНОЕИМЯ( );
ЗАПРОС( "CREATE TABLE " + ИмяТаблицыСтрокЧека + " ( operation varchar( 20 ), password varchar( 50 ), mode int, checktype varchar( 20 ), kodn varchar( 20 ),
													name varchar( 1024 ), quantity float, price float, summa float, _price_u float, department int, destination int, 
													discounttype float, discountvalue float, closetype varchar( 20 ), echeque bit DEFAULT 0,
													utm_code char( 10 ), egais_url varchar( 250 ), egais_sign varchar( 250 ), identity_column int IDENTITY( 1, 1 ), 
													rsubject varchar( 20 ), rvariant varchar( 20 ), marktype char(10), markbc varchar(max), clientcode varchar(10), ffd_version varchar(10), kmcheckresult varchar(1000))" );


//выберем данные шапки документа
ДОБАВИТЬКОНТЕКСТ( "SELECT ndok, date, firm, cur, sklad, kskod, seller, client, partner, cardnumber, checktype, prnonecr, f_fiscal, doccode, f_separatedoc, 
				subtotal, total, cash, noncash, credit, sert, bonus, cashreturn, chequediscount, chequediscount_prc, telemail, author, shiftnumber, chequenumber, comment
				FROM chequelist WHERE identity_column = '" + ЧЕКИДЕНТИФИКАТОР + "'", "ДанныеДокумента" );
ВЫБРАТЬКОНТЕКСТ("ДанныеДокумента");

ДОКУМЕНТ_СУММАПОДИТОГ = ДанныеДокумента.subtotal;
ДОКУМЕНТ_СУММАИТОГ = ДанныеДокумента.total;
ДОКУМЕНТ_СУММАНАЛИЧНЫЕ = ДанныеДокумента.cash;
ДОКУМЕНТ_СУММАБЕЗНАЛИЧНЫЕ = ДанныеДокумента.noncash;
ДОКУМЕНТ_СУММАКРЕДИТ = ДанныеДокумента.credit;
ДОКУМЕНТ_СУММАПРЕДОПЛАТА = ДанныеДокумента.sert;
ДОКУМЕНТ_СУММАБОНУС = ДанныеДокумента.bonus;
ДОКУМЕНТ_СУММАСДАЧА = ДанныеДокумента.cashreturn;
ДОКУМЕНТ_СУММАСКИДКИ = ДанныеДокумента.chequediscount;
ДОКУМЕНТ_СУММАСКИДКИПРОЦЕНТ = ДанныеДокумента.chequediscount_prc;
ДОКУМЕНТ_ТЕЛЕФОНПОЧТА = ДанныеДокумента.telemail;
ДОКУМЕНТ_АВТОР = ДанныеДокумента.author;
ДОКУМЕНТ_ДАТА = ДанныеДокумента.date;
ДОКУМЕНТ_КОММЕНТАРИЙ = ДанныеДокумента.comment;
ДОКУМЕНТ_ФИРМА = ДанныеДокумента.firm;
ДОКУМЕНТ_СКЛАД = ДанныеДокумента.sklad;
ДОКУМЕНТ_ПРОДАВЕЦ = ДанныеДокумента.seller;
ДОКУМЕНТ_ВЛАДЕЛЕЦКАРТЫ = ДанныеДокумента.client;
ДОКУМЕНТ_КОНТРАГЕНТ = ДанныеДокумента.partner;
ДОКУМЕНТ_КАРТА = ДанныеДокумента.cardnumber;
ДОКУМЕНТ_ТИПЧЕКА = ДанныеДокумента.checktype;


//Тип чека = 0 Продажа (приход)
//Тип чека = 1 Возврат (возврат прихода)
//Тип чека = 2 Покупка (Расход)
//Тип чека = 3 Возврат покупки (Возврат расхода)
//Тип чека = 4 Внесение
//Тип чека = 5 Выплата
//Тип чека = 6 Прочее (Нефинансовый)
//Тип чека = 8 Открытие смены
//Тип чека = 10 Закрытие смены

ТипЧекаПечать	= "ПРОДАЖА";

IF ( VAL(ДОКУМЕНТ_ТИПЧЕКА) == 0 )
	ТипЧекаПечать	= "ПРОДАЖА";
ELSE IF ( VAL(ДОКУМЕНТ_ТИПЧЕКА) == 1 )
	ТипЧекаПечать	= "ВОЗВРАТ";
ELSE IF ( VAL(ДОКУМЕНТ_ТИПЧЕКА) == 2 )
	ТипЧекаПечать	= "РАСХОД";
ELSE IF ( VAL(ДОКУМЕНТ_ТИПЧЕКА) == 3 )
	ТипЧекаПечать	= "ВОЗВРАТ РАСХОДА";
ELSE IF ( VAL(ДОКУМЕНТ_ТИПЧЕКА) == 4 )
	ТипЧекаПечать	= "ВНЕСЕНИЕ";
ELSE IF ( VAL(ДОКУМЕНТ_ТИПЧЕКА) == 5 )
	ТипЧекаПечать	= "ВЫПЛАТА";
ELSE IF ( VAL(ДОКУМЕНТ_ТИПЧЕКА) == 6 )
	ТипЧекаПечать	= "ТОВАРНЫЙ ЧЕК";
ELSE IF ( VAL(ДОКУМЕНТ_ТИПЧЕКА) == 8 )
	ТипЧекаПечать	= "ОТКРЫТИЕ СМЕНЫ";
ELSE IF ( VAL(ДОКУМЕНТ_ТИПЧЕКА) == 10 )
	ТипЧекаПечать	= "ЗАКРЫТИЕ СМЕНЫ";
	
	
IF ( TYPE("ВЕРСИЯФФД") == "U" )	
	ВЕРСИЯФФД = "1.05";

//отркытие чека
//при этом не будем передавать значение 0 через поле checktype, т.к. дальнейшую печать чека по временной таблице будет осуществлять функция АТОЛДТО10_ПечатьНефискальногоЧекаНаФР, которая всегда формирует нефискальный документ
ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination, echeque, clientcode, ffd_version )
		 VALUES ( 'Открыть чек', '" + STDF( FROperatorProfile )+ "', 1, '" + ТипЧекаПечать + "', '', 0, 0, 0, 0, '" + ДОКУМЕНТ_КОНТРАГЕНТ + "', '" + ВЕРСИЯФФД + "' )" );

СтрокаТипЧека = ФР.ФормированиеСтрокиДляНефискальнойПечати("ОПЕРАЦИЯ", "LR", "", "" + ТипЧекаПечать + "", "");
ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination)
		 VALUES ( 'Печать строки', '', '', 0, '" + СтрокаТипЧека + "', 0, 0, 0 )" );

//покупатель в чеке
НаименованиеКлиента = "";
ИННКлиента 			= "";
IF ( !ПУСТО( ДОКУМЕНТ_КОНТРАГЕНТ ) )
{
	ДОБАВИТЬКОНТЕКСТ("SELECT priz, name, inn FROM sprclient WHERE code = '" + ALLTRIM(ДОКУМЕНТ_КОНТРАГЕНТ) + "'", "ДанныеКлиента", 1);
	ВЫБРАТЬКОНТЕКСТ("ДанныеКлиента");
	ТипКлиента = VAL(ДанныеКлиента.priz);
	IF (ТипКлиента == 1 OR ТипКлиента == 3)
	{
		НаименованиеКлиента = ФР.XMLTRANS(ДанныеКлиента.name);
		ИННКлиента = ДанныеКлиента.inn;
	}
	УДАЛИТЬКОНТЕКСТ("ДанныеКлиента");
	ВЫБРАТЬКОНТЕКСТ("ДанныеДокумента");
}
IF ( !ПУСТО( НаименованиеКлиента ) )
{
	СтрокаЗаголовокКлиента = ФР.ФормированиеСтрокиДляНефискальнойПечати("КЛИЕНТ", "L", "", "", "");
	ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination)
		 VALUES ( 'Печать строки', '', 0, 0, '" + СтрокаЗаголовокКлиента + "', 0, 0, 0 )" );
	СтрокаНаименованиеКлиента = ФР.ФормированиеСтрокиДляНефискальнойПечати("" + ALLTRIM(НаименованиеКлиента), "L", "", "", "");
	ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination)
		 VALUES ( 'Печать строки', '', 3, 0, '" + ФР.XMLTRANS(СтрокаНаименованиеКлиента) + "', 0, 0, 0 )" );	 
}
IF ( !ПУСТО( ИННКлиента ) )
{
	СтрокаИННКлиента = ФР.ФормированиеСтрокиДляНефискальнойПечати("ИНН КЛИЕНТА", "LR", "", "" + ALLTRIM(ИННКлиента) + "", "");	
	ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination)
		 VALUES ( 'Печать строки', '', 0, 0, '" + СтрокаИННКлиента + "', 0, 0, 0 )" );
}

//напечатаем версию ФФД
СтрокаВерсияФФД = ФР.ФормированиеСтрокиДляНефискальнойПечати("Версия ФФД", "LR", "", "" + ALLTRIM(ВЕРСИЯФФД) + "", "");	
ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination)
		 VALUES ( 'Печать строки', '', 0, 0, '" + СтрокаВерсияФФД + "', 0, 0, 0 )" );

	 
//выведем кассира в чеке
НаименованиеКассира = ЗАПРОС("SELECT name FROM sprempl WHERE code = '" + ДОКУМЕНТ_ПРОДАВЕЦ + "'");
СтрокаНаименованиеКассира = ФР.ФормированиеСтрокиДляНефискальнойПечати("Кассир:", "LR", "", "" + ALLTRIM(НаименованиеКассира) + "", "");
ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination)
		 VALUES ( 'Печать строки', '', 0, 0, '" + СтрокаНаименованиеКассира + "', 0, 0, 0 )" );

//номера чека и смены
СтрокаНомерСмены = ДанныеДокумента.shiftnumber;
СтрокаНомерЧека = ДанныеДокумента.chequenumber;

СтрокаНомерСменыИЧека = ФР.ФормированиеСтрокиДляНефискальнойПечати("Смена № " + СтрокаНомерСмены, "LR", "", "Чек № " + СтрокаНомерЧека, "");
ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination)
		 VALUES ( 'Печать строки', '', 0, 0, '" + СтрокаНомерСменыИЧека + "', 0, 0, 0 )" );
		 
//дата чека
СтрокаДатаЧека = ФР.ФормированиеСтрокиДляНефискальнойПечати("Дата", "LR", "", "" + ДОКУМЕНТ_ДАТА, "");
ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination)
		 VALUES ( 'Печать строки', '', 0, 0, '" + СтрокаДатаЧека + "', 0, 0, 0 )" );

//разделительная линия
ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination)
		 VALUES ( 'Печать строки', '', 0, 0, '================================================', 0, 0, 0 )" );
//удаляем контекст
УДАЛИТЬКОНТЕКСТ("ДанныеДокумента");


//позиции чека
ДОБАВИТЬКОНТЕКСТ("SELECT ic, nn, s_code, nnname, kolp, cena, summa, cena_sale, summa_sale, cena_wd, summa_wd, barcode, ed, koef_e, koef_c 
				FROM chequespec 
				WHERE ic = '" + ЧЕКИДЕНТИФИКАТОР + "'", "ПозицииЧека" );
ВЫБРАТЬКОНТЕКСТ( "ПозицииЧека" );
WHILE ( !КОНЕЦКОНТЕКСТА( "ПозицииЧека" ) )
{
	ПОЗИЦИЯ_КОД = ЕСЛИ( !ПУСТО( ПозицииЧека.nn ), ПозицииЧека.nn, ПозицииЧека.s_code);
	ПОЗИЦИЯ_НАИМЕНОВАНИЕ = ПозицииЧека.nnname; 
	ПОЗИЦИЯ_КОЛИЧЕСТВО = ПозицииЧека.kolp;
	ПОЗИЦИЯ_ЦЕНА = ПозицииЧека.cena;
	ПОЗИЦИЯ_СУММА = ПозицииЧека.summa;
	ПОЗИЦИЯ_ЦЕНАПРОДАЖИ = ПозицииЧека.cena_sale;
	ПОЗИЦИЯ_СУММАПРОДАЖИ = ПозицииЧека.summa_sale;
	ПОЗИЦИЯ_ЦЕНАСОСКИДКОЙ = ПозицииЧека.cena_wd;
	ПОЗИЦИЯ_СУММАСОСКИДКОЙ = ПозицииЧека.summa_wd;
	ПОЗИЦИЯ_ЕДИНИЦА = ПозицииЧека.ed;
	ПОЗИЦИЯ_ШТРИХКОД = ПозицииЧека.barcode;
	
	ПОЗИЦИЯ_СУММАСКИДКИ = ПОЗИЦИЯ_СУММАПРОДАЖИ - ПОЗИЦИЯ_СУММАСОСКИДКОЙ;
	
	
	СекцияККМ				= VAL( ЗАПРОС( "SELECT kkm_section  FROM sprres_g sprres_g
										INNER JOIN sprres sprres ON sprres.group_ = sprres_g.code
										WHERE sprres.code= '" + ПОЗИЦИЯ_КОД + "'" ) );
	КодНалога = "";
	КодНалогаАйтида			= ЗАПРОС( "SELECT kodn FROM sprres WHERE sprres.code = '" + ПОЗИЦИЯ_КОД + "'" );
	КодНалогаККМ			= ЗАПРОС("SELECT sprkod.ex_code AS ex_code FROM sprkodn sprkod
									INNER JOIN speclistkodn spec ON spec.list = sprkod.code
									INNER JOIN sprlistkodn sprlist ON sprlist.code = spec.code
									WHERE sprlist.code = '" + КодНалогаАйтида + "'");
	//определяем налог в ККМ по коду налога в Айтиде
	IF ( МОДЕЛЬОБОРУДОВАНИЯ == "АТОЛДТО10" )
		КодНалога			= ФР.АТОЛДТО10_ПолучитьКодНалогаВККМ( КодНалогаАйтида );
	ELSE IF ( МОДЕЛЬОБОРУДОВАНИЯ == "МЕРКУРИЙ" )
		КодНалога			= ФР.МЕРКУРИЙФР_ПолучитьКодНалогаВККМ( КодНалогаАйтида );
	ELSE IF ( МОДЕЛЬОБОРУДОВАНИЯ == "ШТРИХ" )
		КодНалога			= ФР.ШТРИХ_ПолучитьНалогККМПоТипуНалога( КодНалогаАйтида );	
			
	//если подарочная карта без привязки к товару, то подменим предмет и способ расчета
	IF ( ПУСТО( ПОЗИЦИЯ_КОД ) AND LEFT( ПОЗИЦИЯ_ШТРИХКОД, 6 ) == "PCARD:" )
	{
		ПредметРасчетаАйтида = 10; //платеж
		СпособРасчетаАйтида = 1; //аванс	
	}
	ELSE IF ( ПУСТО( ПОЗИЦИЯ_КОД ) AND LEFT( ПОЗИЦИЯ_ШТРИХКОД, 6 ) <> "PCARD:" )
	{
		ПредметРасчетаАйтида = 1; //товар
		СпособРасчетаАйтида = 2; //полный расчет
	}
	ELSE
	{
		ДОБАВИТЬКОНТЕКСТ( "SELECT ISNULL( sprres_g.rsubject, 0 ) AS rsubject, ISNULL( sprres_g.paytype, 0 ) AS paytype, sprres.f_mark AS f_mark 
					   FROM sprres sprres
					   LEFT OUTER JOIN sprres_g sprres_g ON sprres.group_ = sprres_g.code
					   WHERE sprres.code = '" + ПОЗИЦИЯ_КОД + "'","ТоварИнфо",1);
		ВЫБРАТЬКОНТЕКСТ( "ТоварИнфо" );			   
		ПредметРасчетаАйтида 	= VAL( ТоварИнфо.rsubject );
			
		IF ( VAL( ТоварИнфо.paytype ) == 1 )
			СпособРасчетаАйтида 	= 3;
		ELSE IF ( VAL( ТоварИнфо.paytype ) == 2 )
			СпособРасчетаАйтида 	= 4;
		ELSE
			СпособРасчетаАйтида 	= 4;
			
		МаркированныйТовар		= ТоварИнфо.f_mark;
		УДАЛИТЬКОНТЕКСТ("ТоварИнфо");
		ВЫБРАТЬКОНТЕКСТ( "ПозицииЧека" );
	}

	ПредметРасчета 			= СООТВЕТСТВИЕПРЕДМЕТОВРАСЧЕТА[ ПредметРасчетаАйтида ];
	СпособРасчета 			= СООТВЕТСТВИЕСПОСОБОВРАСЧЕТА[ СпособРасчетаАйтида ]; 
	
	ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, 
													name, quantity, price, _price_u, summa, destination, department, discounttype, discountvalue, kodn, rsubject, rvariant, marktype, markbc, ffd_version )
		 VALUES ( 'Регистрация', '" + FROperatorProfile + "', 1, 0, '" + STDF(ПОЗИЦИЯ_НАИМЕНОВАНИЕ) + "', " + STR( ПОЗИЦИЯ_КОЛИЧЕСТВО, 16, 3 ) + ", " + 
		 STR( ПОЗИЦИЯ_ЦЕНАСОСКИДКОЙ, 16, 2 ) + ", " + STR( ПОЗИЦИЯ_ЦЕНА, 16, 2 ) + ", " + STR( ПОЗИЦИЯ_СУММАСОСКИДКОЙ, 16, 2 ) + ", 0, 1, 0, " + 
		 STR( ПОЗИЦИЯ_СУММАСКИДКИ, 16, 3 ) + ", '" + КодНалога + "', '" + ПредметРасчета + "', '" + СпособРасчета + "', '', '', '" + ВЕРСИЯФФД + "' )" );
	
	ПРОПУСТИТЬ( 1, "ПозицииЧека" );
}
УДАЛИТЬКОНТЕКСТ( "ПозицииЧека" );

//разделительная линия
ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination)
		 VALUES ( 'Печать строки', '', 0, 0, '================================================', 0, 0, 0 )" );
		 
//итог
ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation,price)
			 VALUES ( 'ИТОГ', " + STR( ДОКУМЕНТ_СУММАИТОГ, 16, 2 ) + " )" );
			 
//разделительная линия
ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, password, mode, checktype, name, quantity, price, destination)
		 VALUES ( 'Печать строки', '', 0, 0, '================================================', 0, 0, 0 )" );

//оплаты чека
IF ( ROUND( ДОКУМЕНТ_СУММАНАЛИЧНЫЕ, 2 ) != 0 )
{
	ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, closetype, price )
			VALUES ( 'Оплата', '" + ФР.ККТ_ПолучитьКодОплатыПоТипуОплаты( 0, МОДЕЛЬОБОРУДОВАНИЯ ) + "', " + STR( ДОКУМЕНТ_СУММАНАЛИЧНЫЕ + ДОКУМЕНТ_СУММАСДАЧА, 16, 2 ) + " )" );
}
IF ( ROUND( ДОКУМЕНТ_СУММАБЕЗНАЛИЧНЫЕ, 2 ) != 0 )
{
	ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, closetype, price )
			VALUES ( 'Оплата', '" + ФР.ККТ_ПолучитьКодОплатыПоТипуОплаты( 1, МОДЕЛЬОБОРУДОВАНИЯ ) + "', " + STR( ДОКУМЕНТ_СУММАБЕЗНАЛИЧНЫЕ, 16, 2 ) + " )" );
}
IF ( ROUND( ДОКУМЕНТ_СУММАПРЕДОПЛАТА, 2 ) != 0 )
{
	ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, closetype, price )
			VALUES ( 'Оплата', '" + ФР.ККТ_ПолучитьКодОплатыПоТипуОплаты( 2, МОДЕЛЬОБОРУДОВАНИЯ ) + "', " + STR( ДОКУМЕНТ_СУММАПРЕДОПЛАТА, 16, 2 ) + " )" );
}
IF ( ROUND( ДОКУМЕНТ_СУММАКРЕДИТ, 2 ) != 0 )
{
	ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, closetype, price )
			VALUES ( 'Оплата', '" + ФР.ККТ_ПолучитьКодОплатыПоТипуОплаты( 3, МОДЕЛЬОБОРУДОВАНИЯ ) + "', " + STR( ДОКУМЕНТ_СУММАКРЕДИТ, 16, 2 ) + " )" );
}
/*	
IF ( !ЭТОРМК )
{
	IF ( ROUND( ВСЕГОПРОЧАЯОПЛАТА, 2 ) != 0 )
		ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, closetype, price )
				VALUES ( 'Оплата', '" + ФР.ККТ_ПолучитьКодОплатыПоТипуОплаты( 4, МОДЕЛЬОБОРУДОВАНИЯ ) + "', " + STR( ВСЕГОПРОЧАЯОПЛАТА, 16, 2 ) + " )" );
}
*/


ЗАПРОС( "INSERT INTO " + ИмяТаблицыСтрокЧека + " ( operation, price )
			VALUES ( 'Закрытие чека', " + STR( 0, 16, 2 ) + " )" );

IF ( МОДЕЛЬОБОРУДОВАНИЯ == "АТОЛДТО10" )
	РезультатПечатиНефискальногоЧека		= ФР.АТОЛДТО10_ПечатьНефискальногоЧекаНаФР( );

IF (РезультатПечатиНефискальногоЧека == false)
	_ERROR = true;

_ЭТОКОПИЯЧЕКА = false;

RETURN РезультатПечатиНефискальногоЧека;
