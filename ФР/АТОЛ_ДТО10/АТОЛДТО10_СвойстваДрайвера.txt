
// Свойства драйвера ФР.
COMPARAMORDER( false );
	
_ОШИБКАОТКРЫТИЯДРАЙВЕРА = false;
// В связи с тем, что метод может быть вызван до инициализации драйвера (из карточки оборудования)
// надо обработать ситуацию, когда объект драйвера еще не создан
IF ( ПУСТО( КОДОБОРУДОВАНИЯ ) )
{
	СООБЩЕНИЕ( "Перед настройкой драйвера оборудования необходимо записать карточку торгового оборудования." );
	RETURN false;
}

// Показать свойства
try
{
	fptr	= СОЗДАТЬОБЪЕКТ( "AddIn.Fptr10", 1 );
}
catch( )
{
	fptr = false;
}
IF ( ТИП( "fptr" ) <> "O" )
{
	СООБЩЕНИЕ( "Не удалось создать объект драйвера." + CHR( 13 ) + "Возможно не установлен драйвер ККТ АТОЛ версии 10 (32х разрядный).","Ошибка загрузки драйвера");
	RETURN false;
}
	
НастройкиККТ = ЗАПРОС("SELECT TOP 1 value FROM param_ex WHERE param = 'ATOLKKTSETTINGS" + КОДОБОРУДОВАНИЯ + "'");

IF ( !ПУСТО( НастройкиККТ ) )
{
	//передаем настройки в драйвер 
	fptr.setSettings( НастройкиККТ );
}

try
{
	IF ( fptr.ShowProperties(fptr.LIBFPTR_GUI_PARENT_NATIVE, 0 ) == 0 )
	{
		НастройкиККТ = fptr.getSettings();
		//перезапишем параметр с настройками ККТ
		ЗАПРОС("DELETE FROM param_ex WHERE param = 'ATOLKKTSETTINGS" + КОДОБОРУДОВАНИЯ + "'");		
		ЗАПРОС("INSERT INTO param_ex (param, value) VALUES ('ATOLKKTSETTINGS" + КОДОБОРУДОВАНИЯ + "', '" + НастройкиККТ + "')");
	}
	ELSE
	{
		_ОШИБКАОТКРЫТИЯДРАЙВЕРА = true;
	}
}
catch()
{
	_ОШИБКАОТКРЫТИЯДРАЙВЕРА = true;
}

IF (_ОШИБКАОТКРЫТИЯДРАЙВЕРА == true)
{
	СООБЩЕНИЕ("Не удалось открыть окно настроек драйвера");
	///*
	ВОПРОС = "Инициализировать сохраненные настройки связи значениями поумолчанию?";
	IF (СООБЩЕНИЕ(Вопрос,"Настройка связи с ККТ", 4) == 6)
	{
		//удалим параметр с настройками ККТ
		ЗАПРОС("DELETE FROM param_ex WHERE param = 'ATOLKKTSETTINGS" + ALLTRIM(КОДОБОРУДОВАНИЯ) + "'");
		СООБЩЕНИЕ("Сохраненные настройки ККТ удалены. Попытайтесь открыть диалоговое окно драйвера еще раз.");
	}
	//*/
}

fptr = false;
