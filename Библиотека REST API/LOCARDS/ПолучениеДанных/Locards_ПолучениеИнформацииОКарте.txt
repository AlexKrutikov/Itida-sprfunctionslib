IF ( ПУСТО( НОМЕРКАРТЫ ) AND ПУСТО( НОМЕРТЕЛЕФОНА ) )
{
	СООБЩЕНИЕ("Не определен номер карты или телефон клиента. " + CHR(13) + CHR(10) + "Информация по карте загружена не будет.", "Locards - получение данных");
	RETURN "";
}

Локардс_Токен = RESTAPI.LOCARDS_TOKEN( НОМЕРКАРТЫ );
IF ( ПУСТО(Локардс_Токен) )
{
	СООБЩЕНИЕ("Не определен токен LOCARDS. " + CHR(13) + CHR(10) + "Информация по карте загружена не будет.", "Locards - получение данных");
	RETURN "";
}

ИнформацияОКарте = "";
УспешноеПолучение = false;
Локардс_АдресСервера = "https://api.lo.cards";

IF ( !ПУСТО( НОМЕРКАРТЫ ) )
	АдресРесурса = "/v1/crm/card/" + ALLTRIM( НОМЕРКАРТЫ );
ELSE IF ( !ПУСТО( НОМЕРТЕЛЕФОНА ) )
	АдресРесурса = "/v1/crm/card/search?phone=" + ALLTRIM( НОМЕРТЕЛЕФОНА );	

Заголовки[0] = "Authorization: Bearer " + ALLTRIM( Локардс_Токен );	
Заголовки[1] = "Content-Type: application/json; charset=utf-8";	
	
ОТВЕТ_локардс = RESTAPI.LOCARDS_GETЗАПРОС( Локардс_АдресСервера, АдресРесурса, "Заголовки" );

IF ( !ПУСТО( ОТВЕТ_локардс) AND ОТВЕТ_локардс != false )
{
	УспешноеПолучение = ПОЛЕ_JSON( ОТВЕТ_локардс, "success", false);
	IF ( УспешноеПолучение == true OR УспешноеПолучение == "true" )
		ИнформацияОКарте = ПОЛЕ_JSON( ОТВЕТ_локардс, "data", "");	
}

IF ( ПЕРЕМЕННАЯ("ВЫВОДИТЬСООБЩЕНИЕ", false) == true AND !ПУСТО( ИнформацияОКарте ) )
{
	Фамилия = ПОЛЕ_JSON( ИнформацияОКарте, "lastName", "");
	Имя = ПОЛЕ_JSON( ИнформацияОКарте, "firstName", "");
	Отчество = ПОЛЕ_JSON( ИнформацияОКарте, "middleName", "");
	НазваниеШаблона = ПОЛЕ_JSON( ИнформацияОКарте, "templateName", "");
	ДРКлиента = ПОЛЕ_JSON( ИнформацияОКарте, "birthday", "");
	СкидкаКарты = ПОЛЕ_JSON( ИнформацияОКарте, "discount", 0);
	КешбэкКарты = ПОЛЕ_JSON( ИнформацияОКарте, "cashback", 0);
	КоличествоБонусов = STR( ПОЛЕ_JSON( ИнформацияОКарте, "balance", 0) , 10, 2 );

	СимвПС = CHR(13) + CHR(10);
	ТекстСообщения = "";
	ТекстСообщения += "Вид: " + ALLTRIM( НазваниеШаблона ) + СимвПС;
	ТекстСообщения += "Клиент: " + Фамилия + " " + Имя + " " + Отчество + СимвПС;
	ТекстСообщения += "Скидка по карте: " + СкидкаКарты + "%" + СимвПС;
	ТекстСообщения += "Кешбэк по карте: " + КешбэкКарты + "%" + СимвПС;
	ТекстСообщения += "Бонусов: " + КоличествоБонусов + СимвПС;
	СООБЩЕНИЕ( ТекстСообщения, "Данные карты");
	
	//чтобы не выводилось сообщение повторно, меняем значение переменной, после того как вывели сообщение
	ВЫВОДИТЬСООБЩЕНИЕ = false;
}

RETURN ИнформацияОКарте;
