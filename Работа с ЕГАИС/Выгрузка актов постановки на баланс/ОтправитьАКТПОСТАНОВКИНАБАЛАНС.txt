
// Проверяем наличие необходимых реквизитов в товаре, контрагентах и фирме
ДОБАВИТЬКОНТЕКСТ( "SELECT spr.ndok, spr.date, spec.nnname, sprnn.maincode, sprnn.alcocode, sprnn.d_litr, sprnn.nnvid, spec.kolp, sprnn.importer, sprimporter.legal_adress AS importerAddress
				   FROM spr043 spr
				   INNER JOIN spec043 spec ON spr.identity_column = spec.ic
				   INNER JOIN sprnn sprnn ON spec.nn = sprnn.nn
				   LEFT OUTER JOIN sprclient sprimporter ON sprnn.importer = sprimporter.code
				   WHERE spr.identity_column = " + ИДДокумента + " AND ( sprnn.alcocode = '' OR sprnn.d_litr = 0 OR sprnn.nnvid = '' OR sprnn.importer = '' OR spec.kolp <= 0 )", "СписокОшибочныхТоваров" );
				   
IF ( КОЛИЧЕСТВОСТРОК( "СписокОшибочныхТоваров" ) > 0 )
{
	ТекстСообщения	= "В накладной № " + СписокОшибочныхТоваров.ndok + " от " + DTOC( СписокОшибочныхТоваров.date ) + " найдены ошибки."; 

	IF ( КОЛИЧЕСТВОСТРОК( "СписокОшибочныхТоваров" ) > 0 )
	{
		WHILE( !КОНЕЦКОНТЕКСТА( "СписокОшибочныхТоваров" ) )
		{
			Ошибки			=  ЕСЛИ( ПУСТО( СписокОшибочныхТоваров.alcocode ), "не указан алко-код", "" );
			Ошибки			+= ЕСЛИ( ПУСТО( СписокОшибочныхТоваров.d_litr ), ЕСЛИ( !ПУСТО( Ошибки ), ", ", "" ) + "не указан объем тары", "" );
			Ошибки			+= ЕСЛИ( ПУСТО( СписокОшибочныхТоваров.nnvid ), ЕСЛИ( !ПУСТО( Ошибки ), ", ", "" ) + "не указан вид продукции", "" );
			Ошибки			+= ЕСЛИ( ПУСТО( СписокОшибочныхТоваров.importer ), ЕСЛИ( !ПУСТО( Ошибки ), ", ", "" ) + "не указан производитель/импортер", "" );
			Ошибки			+= ЕСЛИ( ПУСТО( СписокОшибочныхТоваров.importerAddress ), ЕСЛИ( !ПУСТО( Ошибки ), ", ", "" ) + "не указан адрес у производителя/импортера", "" );
			Ошибки			+= ЕСЛИ( СписокОшибочныхТоваров.kolp <= 0, ЕСЛИ( !ПУСТО( Ошибки ), ", ", "" ) + "не указано количество продукции", "" );
			ТекстСообщения	+= CHR( 13 ) + "Товар: " + СписокОшибочныхТоваров.maincode + " " + СписокОшибочныхТоваров.nnname + " " + Ошибки + ".";
			ПРОПУСТИТЬ( 1, "СписокОшибочныхТоваров" );
		}
	}
	СООБЩЕНИЕ( ТекстСообщения + CHR( 13 ) + "Документ не был отправлен в ЕГАИС" );
	УДАЛИТЬКОНТЕКСТ( "СписокОшибочныхТоваров" );
	RETURN false;
}
УДАЛИТЬКОНТЕКСТ( "СписокОшибочныхТоваров" );

ЕГАИСИД			= ЗАПРОС( "SELECT value FROM param_ex WHERE param = 'EGAIS_ID" + СКЛАД + "'" );
ДОБАВИТЬКОНТЕКСТ( "SELECT spr043.ndok, spr043.date, spr043.note
					FROM spr043 spr043
					INNER JOIN sprfirm sprfirm ON sprfirm.code = spr043.firm
					WHERE spr043.identity_column = " + ИДДокумента, "ДокументИнфо" );

НомерДокумента			= ЕГАИС.ЗаменитьСимволы( ДокументИнфо.ndok );
ПримечаниеКДокументу	= ЕГАИС.ЗаменитьСимволы( ДокументИнфо.note );
ТекстАкта		= 
"<?xml version=""1.0"" encoding=""UTF-8""?>
	<ns:Documents Version=""1.0""
		xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance""
		xmlns:ns= ""http://fsrar.ru/WEGAIS/WB_DOC_SINGLE_01""
		xmlns:oref=""http://fsrar.ru/WEGAIS/ClientRef""
		xmlns:pref=""http://fsrar.ru/WEGAIS/ProductRef""
		xmlns:ain=""http://fsrar.ru/WEGAIS/ActChargeOn""
		xmlns:iab=""http://fsrar.ru/WEGAIS/ActInventoryABInfo"">
		<ns:Owner>
			<ns:FSRAR_ID>" + ЕГАИСИД + "</ns:FSRAR_ID>
		</ns:Owner>
		<ns:Document>
			<ns:ActChargeOn>
				<ain:Header>
					<ain:Number>" + НомерДокумента + "</ain:Number>
					<ain:ActDate>" + DTOC( ДокументИнфо.date, 7,  "-" ) + "</ain:ActDate>
					<ain:Note>" + ПримечаниеКДокументу + "</ain:Note>
				</ain:Header>
				<ain:Content>
";
УДАЛИТЬКОНТЕКСТ( "ДокументИнфо" );

// Выбираем строки накладной для передачи в ЕГАИС
ДОБАВИТЬКОНТЕКСТ( "SELECT spec.nn, spec.alcocode, spec.npp, spec.kolp, spec.informAID, spec.informBID, spec.informAKolp, spec.bottlingdate, 
						  CONVERT( bit, CASE WHEN spec.ed IN ( 'дал', 'дкл' ) THEN 1 ELSE 0 END ) AS unpacked,
						  spec.ttnnumber, spec.TTNDate, spec.EGAISFixNumber, spec.EGAISFixDate, spec.identity_column
				   FROM spec043 spec
				   INNER JOIN sprnn sprnn ON sprnn.nn = spec.nn
				   LEFT OUTER JOIN sprclient sprclient ON sprnn.importer = sprclient.code
				   WHERE spec.ic = " + ИДДокумента + " 
				   ORDER BY dbo.fn_chartoint( npp ) ", "СписокСтрок" );
WHILE ( !КОНЕЦКОНТЕКСТА( "СписокСтрок" ) )
{
	ТекстАкта	+= 
"
					<ain:Position>
						<ain:Identity>" + STR( НОМЕРСТРОКИ( "СписокСтрок" ) + 1 ) + "</ain:Identity>
						" + ЕГАИС.ВыгрузитьТовар( СписокСтрок.alcocode, "ain:Product", СписокСтрок.nn, СписокСтрок.unpacked ) + 
						"
						<ain:Quantity>" + STR( СписокСтрок.kolp, 16, 3 ) + "</ain:Quantity>
";

	// В зависимости от того, какие поля указаны формируем разные разделы для справки А и Б
	IF ( !ПУСТО( СписокСтрок.informAID ) OR !ПУСТО( СписокСтрок.informBID ) )
		ТекстАкта	+= 
"						<ain:InformAB>
							<ain:InformABKey>
								<ain:FormA>" + СписокСтрок.informAID + "</ain:FormA>
								<ain:LastFormB>" + СписокСтрок.informBID + "</ain:LastFormB>
							</ain:InformABKey>
						</ain:InformAB>
";
	ELSE
		ТекстАкта	+= 
"						<ain:InformAB>
							<ain:InformABReg>
								<ain:InformA>
									<iab:Quantity>" + STR( СписокСтрок.informAKolp ) + "</iab:Quantity>
									<iab:BottlingDate>" + DTOC( СписокСтрок.bottlingdate, 7, "-" ) + "</iab:BottlingDate>
									<iab:TTNNumber>" + ЕГАИС.ЗаменитьСимволы( СписокСтрок.ttnnumber ) + "</iab:TTNNumber>
									<iab:TTNDate>" + DTOC( СписокСтрок.TTNDate, 7, "-" ) + "</iab:TTNDate> 
									" + ЕСЛИ( !ПУСТО( СписокСтрок.EGAISFixNumber ), "<iab:EGAISFixNumber>" + ЕГАИС.ЗаменитьСимволы( СписокСтрок.EGAISFixNumber ) + "</iab:EGAISFixNumber>", "" ) +
									    ЕСЛИ( ГОД( СписокСтрок.EGAISFixDate ) >= 2000, "<iab:EGAISFixDate>" + DTOC( СписокСтрок.EGAISFixDate, 7, "-" ) + "</iab:EGAISFixDate>", "" ) + "
								</ain:InformA>
							</ain:InformABReg>
						</ain:InformAB>
";

	// Теперь выводим акцизные марки
	ДОБАВИТЬКОНТЕКСТ( "SELECT barcode FROM barcodes WHERE code = '043' AND spec_ic = " + СписокСтрок.identity_column, "СписокМарок" );
	IF ( КОЛИЧЕСТВОСТРОК( "СписокМарок" ) > 0 )
	{
		ТекстАкта	+= 
"						<ain:MarkCodeInfo>
";
		WHILE ( !КОНЕЦКОНТЕКСТА( "СписокМарок" ) )
		{
			ТекстАкта	+= 
"							<ain:MarkCode>" + ЕГАИС.ЗаменитьСимволы( СписокМарок.barcode ) + "</ain:MarkCode> 
";
			ПРОПУСТИТЬ( 1,  "СписокМарок" );
		}
		УДАЛИТЬКОНТЕКСТ( "СписокМарок" );
		ТекстАкта	+= 
"						</ain:MarkCodeInfo>		
"
	}
	ТекстАкта	+= 
"					</ain:Position>
";
	ПРОПУСТИТЬ( 1, "СписокСтрок" );
}
УДАЛИТЬКОНТЕКСТ( "СписокСтрок" ); 

ТекстАкта		+=
"				</ain:Content>
			</ns:ActChargeOn>
		</ns:Document>
	</ns:Documents>";
				
// Отправляем получившийся текст на сервер
ОтветСервера = "";
IF ( !ЕГАИС.HTTPCommand( "POST", "/opt/in/ActChargeOn", ПЕРЕКОДИРОВАТЬ( ТекстАкта, "ANSI", "UTF-8" ), @ОтветСервера, "ActChargeOn.xml" ) ) RETURN false; 

ДОБАВИТЬКОНТЕКСТ( "SET FMTONLY ON SELECT CONVERT( char( 250 ), '' ) AS url SET FMTONLY OFF", "АктUrl" );
ЗАГРУЗИТЬ( "АктUrl", "XMLSTRING", ОтветСервера, "url" );	

// Если все благополучно отправлено, то сохраняем информацию в документе
ЗАПРОС( "UPDATE spr043 SET egaisstatus = 2, egaisreplyid= '" + STDF( АктUrl.url ) + "', egaiscomment= '' WHERE identity_column = " + ИДДокумента );
ДОБАВИТЬКОНТЕКСТ( "INSERT INTO spec000_img ( code, ic, pnum, filename, doc_image ) 
				   VALUES ( '043', " + ИДДокумента + ", 0, 'АктКвитанцияУТМ.txt', CONVERT( varbinary( max ), '" + STDF( ОтветСервера ) + "' ) )", "ПриложениеКДокументу" );
УДАЛИТЬКОНТЕКСТ( "ПриложениеКДокументу" );
УДАЛИТЬКОНТЕКСТ( "АктUrl" );
ЕГАИС.ДобавитьЗапрос( 6, "043", ИДДокумента, ТекстАкта, ОтветСервера );

RETURN true;
