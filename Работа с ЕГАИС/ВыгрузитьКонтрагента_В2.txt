

IF ( !ФИРМАИЛИКОНТРАГЕНТ )
	ДОБАВИТЬКОНТЕКСТ( "SELECT inn, kpp, name, CASE WHEN shortname = '' THEN name ELSE shortname END AS shortname, legal_adress AS address, 
							  ISNULL( ( SELECT MAX( ex_code ) FROM sprcountry WHERE name = sprclient.country ), '' ) AS country, postcode, dbo.fn_chartoint( priz ) AS priz,
							  LEFT( CASE WHEN kpp = '' THEN inn ELSE kpp END, 2 ) AS region, department, city, town, street, building, block, egaisid AS EGAISID
					   FROM sprclient
					   WHERE code = '" + КОНТРАГЕНТ + "'", "КонтрагентИнфо" );
ELSE
{
	// Если у склада указана КПП, то берем его из склада. Иначе из фирмы
	КППСКЛАДА			= ЗАПРОС( "SELECT kpp FROM sprskl WHERE code = (SELECT MAX( CONVERT( varchar( 100 ), value ) ) FROM param_ex WHERE param = 'EGAIS_SKLAD" + УТМ + "' )" );
	АДРЕССКЛАДА			= ЗАПРОС( "SELECT address FROM sprskl WHERE code = (SELECT MAX( CONVERT( varchar( 100 ), value ) ) FROM param_ex WHERE param = 'EGAIS_SKLAD" + УТМ + "' )" );
	ВЫРАЖЕНИЕДЛЯКПП		= "CASE WHEN '" + КППСКЛАДА + "' = '' THEN kpp ELSE '" + КППСКЛАДА + "' END";
	ДОБАВИТЬКОНТЕКСТ( "SELECT inn, " + ВЫРАЖЕНИЕДЛЯКПП + " AS kpp, name, CASE WHEN shortname = '' THEN name ELSE shortname END AS shortname, 
							  CASE WHEN '" + STDF( АДРЕССКЛАДА ) + "' = '' THEN legal_adress ELSE '" + STDF( АДРЕССКЛАДА ) + "' END AS address,
							  ISNULL( ( SELECT MAX( ex_code ) FROM sprcountry WHERE name = sprfirm.country ), '' ) AS country, postcode, 0 AS priz,
							  LEFT( CASE WHEN " + ВЫРАЖЕНИЕДЛЯКПП + " = '' THEN inn ELSE " + ВЫРАЖЕНИЕДЛЯКПП + " END, 2 ) AS region, department, city, town, street, building, block,
							  ( SELECT value FROM param_ex WHERE param = 'EGAIS_ID" + УТМ + "' ) AS EGAISID
					   FROM sprfirm sprfirm
					   WHERE code = '" + КОНТРАГЕНТ + "'", "КонтрагентИнфо" );
}

// Если не указан адрес контрагента, то не выводим ничего, т.к. иначе будет ошибка
СтрокаКонтрагента			= "";
ТЮЛ							= ЕСЛИ( КонтрагентИнфо.priz == 5, "oref:FO", ЕСЛИ( КонтрагентИнфо.priz == 4, "oref:TS",
							  ЕСЛИ( ДЛИНА( КонтрагентИнфо.inn ) > 10, "oref:FL", "oref:UL" ) ) );
ТИНН						= ЕСЛИ( КонтрагентИнфо.priz == 5, "", ЕСЛИ( КонтрагентИнфо.priz == 4, "oref:TSNUM", "oref:INN" ) );

IF ( ПУСТО( КонтрагентИнфо.EGAISID ) )
{
	СООБЩЕНИЕ( "У контрагента " + КонтрагентИнфо.name + " (" + КОНТРАГЕНТ + ") не указан ЕГАИС ИД" + CHR( 13 ) + "Контрагент выгружен не будет" );
	THROW "Контрагент";
}
IF ( ПУСТО( КонтрагентИнфо.address ) )
{
	СООБЩЕНИЕ( "У контрагента " + КонтрагентИнфо.name + " (" + КОНТРАГЕНТ + ") не указан адрес местоположения" + CHR( 13 ) + "Контрагент выгружен не будет" );
	THROW "Контрагент";
}
IF ( ВСПИСКЕ( ТЮЛ, "oref:UL", "oref:FL" ) AND ПУСТО(  КонтрагентИнфо.inn ) )
{
	СООБЩЕНИЕ( "У контрагента " + КонтрагентИнфо.name + " (" + КОНТРАГЕНТ + ") не указан ИНН" + CHR( 13 ) + "Контрагент выгружен не будет" );
	THROW "Контрагент";
}

IF ( ТЮЛ == "oref:UL" AND ( ПУСТО( КонтрагентИнфо.kpp ) OR LEN( КонтрагентИнфо.kpp ) != 9 ) )
{
	СООБЩЕНИЕ( "У контрагента " + КонтрагентИнфо.name + " (" + КОНТРАГЕНТ + ") не указан, или указан неверно КПП" + CHR( 13 ) + "Контрагент выгружен не будет" );
	THROW "Контрагент";
}

IF ( ВСПИСКЕ( ТЮЛ, "oref:FO" ) )
{
	ИЗМЕНИТЬПОЛЕ( "КонтрагентИнфо", "inn", "" );
	ИЗМЕНИТЬПОЛЕ( "КонтрагентИнфо", "kpp", "" );
}
IF ( ВСПИСКЕ( ТЮЛ, "oref:TS", "oref:FL" ) )
	ИЗМЕНИТЬПОЛЕ( "КонтрагентИнфо", "kpp", "" );

IF ( !ПУСТО( КонтрагентИнфо.address ) ) 
	СтрокаКонтрагента			=
							"<" + ТЭГ + "><" + ТЮЛ + ">" + 
								ЕСЛИ( !ПУСТО( КонтрагентИнфо.inn ) AND !ПУСТО( ТИНН )," 
								<" + ТИНН + ">" + КонтрагентИнфо.inn + "</" + ТИНН+ ">", "" ) +
								ЕСЛИ( !ПУСТО( КонтрагентИнфо.kpp )," 
								<oref:KPP>" + КонтрагентИнфо.kpp + "</oref:KPP>", "" ) + "
								<oref:ClientRegId>" + КонтрагентИнфо.EGAISID + "</oref:ClientRegId>
								<oref:FullName>" + ЕГАИС.ЗаменитьСимволы( КонтрагентИнфо.name ) + "</oref:FullName>
								<oref:ShortName>" + ЕГАИС.ЗаменитьСимволы( ЛЕВСИМВ( КонтрагентИнфо.shortname, 64 ) ) + "</oref:ShortName>
								" +	ЕГАИС.ВыгрузитьАдрес_В2( КонтрагентИнфо.country, ЕСЛИ( ВСПИСКЕ( КонтрагентИнфо.priz, 4, 5 ), "", КонтрагентИнфо.region ), КонтрагентИнфо.address ) + "
							</" + ТЮЛ + "></" + ТЭГ + ">";
						
УДАЛИТЬКОНТЕКСТ( "КонтрагентИнфо" );

RETURN СтрокаКонтрагента;
