
СЕРТИФИКАТ				= ЗАПРОС( "SELECT value FROM param_ex WHERE param= 'MARK_CERTIFICATE" + ПРЕДПРИЯТИЕ + "'" );
ТЕСТОВЫЙКОНТУР			= VAL( ЗАПРОС( "SELECT value FROM param_ex WHERE param= 'MARK_APITEST" + ПРЕДПРИЯТИЕ + "'" ) ) != 0 ;
ГРУППАМАРКИРОВКИ		= LOWER( ALLTRIM( ЗАПРОС( "SELECT value FROM param_ex WHERE param= 'MARK_GROUP" + ПРЕДПРИЯТИЕ + "'" ) ) );
ИННФИРМЫ				= ЗАПРОС( "SELECT inn FROM sprfirm WHERE code IN ( SELECT CONVERT( varchar( 10 ), value ) FROM param_ex WHERE param= 'MARK_FIRM" + ПРЕДПРИЯТИЕ + "')" );

ДатаДокумента			= ЗАПРОС( "SELECT date FROM mark_out WHERE identity_column = " + ИДДОКУМЕНТА );
НомерДокумента			= ЗАПРОС( "SELECT ndok FROM mark_out WHERE identity_column = " + ИДДОКУМЕНТА );
ИДДОКУМЕНТА				= ЗАПРОС( "SELECT guid FROM mark_out WHERE identity_column = " + ИДДОКУМЕНТА );
ТипДокумента			= ЗАПРОС( "SELECT doctype FROM mark_out WHERE identity_column = " + ИДДОКУМЕНТА );
ТипДокумента			= ЕСЛИ( ПРОПИСНЫЕ( СЖАТЬПРОБЕЛЫ( ТипДокумента ) ) == "LP_SHIP_GOODS_CROSSBORDER", "LP_CANCEL_SHIPMENT_CROSSBORDER", "LP_CANCEL_SHIPMENT" );
ТекстЗапроса			= "
{

	""cancel_shipment_date"": """ + ПЕРЕКОДИРОВАТЬ( ЗАМЕНИТЬ( НомерДокумента, """", "\""" ), "ANSI", "UTF-8") + """,
	""cancel_shipment_number"": """ + DTOC( ДатаДокумента, 7, "-" ) + "T00:00:00Z"",
	""participant_inn"": """ + ИННФИРМЫ + """,
	""shipment_number"": """ + ИДДОКУМЕНТА + """
}	
";

Ответ					= "";
GUIDДокумента			= "";

// Отправляем запрос в МАРКИРОВКУ
// Авторизуемся
МАРКИРОВКАТОКЕН			=  Маркировка.Авторизация( ПРЕДПРИЯТИЕ, СЕРТИФИКАТ, ТЕСТОВЫЙКОНТУР );
IF ( ПУСТО( МАРКИРОВКАТОКЕН ) ) RETURN false;

АдресСервера 			= ЕСЛИ( ТестовыйКонтур, "https://demo.lp.crpt.tech", "https://ismp.crpt.ru" );
try
{
	ТекстОшибки			= "Сервер " + АдресСервера + CHR( 13 ) + "вернул сообщение об ошибке: " + CHR( 13 );
	Соединение 			= HTTPCONNECT( АдресСервера, "", true, МАРКИРОВКА.ФАЙЛЖУРНАЛА( ) );
	IF ( !МАРКИРОВКА.ПРОВЕРКАНАОШИБКИ( @Ответ ) ) THROW ( Ответ );
	
	Подпись				= ПЕРЕКОДИРОВАТЬ( ШИФРОВАНИЕ( СЕРТИФИКАТ, ТекстЗапроса, 1, true, false, _МАРКИРОВКА_ХРАНИЛИЩЕСЕРТИФИКАТОВ ), "", "BASE64" );
	Документ		 	= "{""product_document"":""" + ПЕРЕКОДИРОВАТЬ( ТекстЗапроса, "", "BASE64" ) + """,
						  ""document_format"": ""MANUAL"", ""type"": """ + ТипДокумента + """, ""signature"":""" + Подпись + """}";

	Заголовки[ 0 ]		= "Content-Type: application/json";
	Заголовки[ 1 ]		= "Authorization: Bearer " + МАРКИРОВКАТОКЕН;
	Ответ				= HTTPPOST( Соединение, "/api/v3/lk/documents/create?pg=" + ГРУППАМАРКИРОВКИ, Документ, "Заголовки" );
	IF ( !МАРКИРОВКА.ПРОВЕРКАНАОШИБКИ( @Ответ ) ) THROW ( Ответ );

	GUIDДокумента		= СЖАТЬПРОБЕЛЫ( Ответ );

	// Запрашиваем статус документа
	Документ			= МАРКИРОВКА.ЗАПРОСДОКУМЕНТАСПРОВЕРКОЙСТАТУСА( ПРЕДПРИЯТИЕ, СЕРТИФИКАТ, ТЕСТОВЫЙКОНТУР, ОТВЕТ );
	IF ( ПУСТО( Документ ) ) 
	{
		IF ( !ПУСТО( ОТВЕТ ) )
			ЗАПРОС( "UPDATE mark_out SET rguid = '" + STDF( Ответ ) + "', status = 4, reply= 'ответ ГИС МТ не был получен' WHERE identity_column = " + ИДДОКУМЕНТА );
		RETURN "";
	}
	
	ЗАГРУЗИТЬJSON( "Документ", Документ );
	// Если документ был принят без ошибок, то ставим статус 1.
	IF ( ПРОПИСНЫЕ( СЖАТЬПРОБЕЛЫ( ЗНАЧЕНИЕПОЛЯ( "Документ", "status", "" ) ) ) == "CHECKED_OK" )
		ЗАПРОС( "UPDATE mark_out SET rguid = '" + STDF( Ответ ) + "', status = 5, reply= 'нет ошибок' WHERE identity_column = " + ИДДОКУМЕНТА );
	ELSE IF ( ПРОПИСНЫЕ( СЖАТЬПРОБЕЛЫ( ЗНАЧЕНИЕПОЛЯ( "Документ", "status", "" ) ) ) == "IN_PROGRESS" )
		THROW( "Сервер ГИС МТ не успел обработать документ за отведенное для этого время." + CHR( 13 ) + "Проверьте статус отправки позже запросив обновления документов" );
	ELSE
	{
		// Иначе, выводим сообщение об ошибке и статус 2
		ЗАГРУЗИТЬJSON( "Ошибка", ЗНАЧЕНИЕПОЛЯ( "Документ", "errors", "" ), "error_message" );
		ТекстОшибки		= ПЕРЕКОДИРОВАТЬ( ЗНАЧЕНИЕПОЛЯ( "Ошибка", "error_message", "" ), "UTF-8", "ANSI" );
		ЗАПРОС( "UPDATE mark_out SET rguid = '" + STDF( Ответ ) + "', status = 4, reply= '" + ЛЕВСИМВ( STDF( ТекстОшибки ), 250 ) + "' WHERE identity_column = " + ИДДОКУМЕНТА );
		THROW( ТекстОшибки );
	}

}
catch ( ТекстСообщения )
{
	HTTPCLOSE( Соединение );
	УДАЛИТЬКОНТЕКСТ( "Документ" );
	УДАЛИТЬКОНТЕКСТ( "Ошибка" );

	СООБЩЕНИЕ( ТекстСообщения );
		
	RETURN "";
}
HTTPCLOSE( Соединение );
УДАЛИТЬКОНТЕКСТ( "Документ" );
RETURN GUIDДокумента;
